apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.github.triplet.play'

if (!project.hasProperty('tmdbApiKey')) {
    throw new GradleException('no tmdbApiKey property found needed to acces the backend server. Please define this property (i.e. in ~.gradle/gradle.properties) and try to build again ')
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    signingConfigs {
        release
    }

    lintOptions {
        disable 'InvalidPackage', 'GoogleAppIndexingWarning'
        textOutput 'stdout'
        textReport true
    }

    defaultConfig {
        applicationId "de.andrestefanov.popularmovies"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "TMBD_API_KEY", "\"$tmdbApiKey\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Butterknife
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    // android support
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:percent:25.1.1'

    // Picasso
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    // FAB
    compile 'com.github.clans:fab:1.6.4'
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

if (keystorePropertiesFile.canRead()) {
    // Initialize a new Properties() object called keystoreProperties.
    def keystoreProperties = new Properties()

    // Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    android.signingConfigs.release.keyAlias = keystoreProperties['keyAlias']
    android.signingConfigs.release.keyPassword = keystoreProperties['keyPassword']
    android.signingConfigs.release.storeFile = file(keystoreProperties['storeFile'])
    android.signingConfigs.release.storePassword = keystoreProperties['storePassword']
} else if (project.hasProperty('keystoreFile')) {
    android.signingConfigs.release.keyAlias = "$keystoreKeyAlias"
    android.signingConfigs.release.keyPassword = "$keystoreKeyPassword"
    android.signingConfigs.release.storeFile = file("$keystoreFile")
    android.signingConfigs.release.storePassword = "$keystorePassword"
} else {
    println 'signing disabled'
    android.buildTypes.release.signingConfig = null
}